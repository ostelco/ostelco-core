plugins {
  id "org.jetbrains.kotlin.jvm" version "1.2.31"
  id "idea"
  id "java-library"
  id "signing"
  id "maven"
}

version = "1.0.0-SNAPSHOT"

dependencies {
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
  implementation "io.dropwizard:dropwizard-logging:$dropwizardVersion"
  implementation "com.google.cloud:google-cloud-logging-logback:0.44.0-alpha"
}

task javadocJar(type: Jar) {
  classifier = 'javadoc'
  from javadoc
}

task sourcesJar(type: Jar) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

artifacts {
  archives javadocJar, sourcesJar
}

signing {
  sign configurations.archives
}

// For developers not responsible for uploadArchives,
// and for CI server, until is it not setup to do so.
signArchives.enabled = hasProperty("ossrhUsername")
if (! hasProperty("ossrhUsername")) {
  ext.ossrhUsername=""
}
if (! hasProperty("ossrhPassword")) {
  ext.ossrhPassword=""
}

uploadArchives.repositories.mavenDeployer {
  beforeDeployment { deployment -> signing.signPom(deployment) }

  repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
    authentication(userName: ossrhUsername, password: ossrhPassword)
  }

  snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
    authentication(userName: ossrhUsername, password: ossrhPassword)
  }

  pom.project {
    name = 'Dropwizard GCP Stack-driver logging'
    packaging 'jar'
    // optionally artifactId can be defined here
    description = 'A library for Dropwizard application to have GCP Stack-driver logging.'
    url 'https://github.com/ostelco/ostelco-core/tree/develop/dropwizard-gcp-stackdriver-logging'

    scm {
      connection 'scm:svn:https://github.com/ostelco/ostelco-core.git'
      developerConnection 'scm:https://github.com/ostelco/ostelco-core'
      url 'https://github.com/ostelco/ostelco-core'
    }

    licenses {
      license {
        name = 'The Apache License, Version 2.0'
        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
      }
    }

    developers {
      developer {
        id 'la3lma'
        name = 'Bj√∏rn Remseth'
        email 'la3lma@gmail.com'
      }
    }
  }
}