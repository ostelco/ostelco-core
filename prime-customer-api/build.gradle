plugins {
  id 'java-library'
  id 'org.hidetake.swagger.generator' version '2.18.1'
  id "idea"
}

// gradle generateSwaggerCode
swaggerSources {
  'java-client' {
    inputFile = file("${projectDir}/../prime/infra/dev/prime-customer-api.yaml")
    code {
      language = 'java'
      configFile = file("${projectDir}/config.json")
    }
  }
}

compileJava.dependsOn swaggerSources.'java-client'.code
sourceSets.main.java.srcDir "${swaggerSources.'java-client'.code.outputDir}/src/main/java"
sourceSets.main.resources.srcDir "${swaggerSources.'java-client'.code.outputDir}/src/main/resources"

dependencies {
  swaggerCodegen "io.swagger:swagger-codegen-cli:$swaggerCodegenVersion"

  implementation "javax.annotation:javax.annotation-api:$javaxAnnotationVersion"

  // taken from build/swagger-code-java-client/build.gradle
  implementation 'io.swagger:swagger-annotations:1.5.23'
  implementation 'com.google.code.gson:gson:2.8.5'
  implementation 'com.squareup.okhttp:okhttp:2.7.5'
  implementation 'com.squareup.okhttp:logging-interceptor:2.7.5'
  implementation 'io.gsonfire:gson-fire:1.8.3'
  implementation 'org.threeten:threetenbp:1.4.0'
  testImplementation 'junit:junit:4.12'
}

idea {
  module {
    // generatedSourceDirs
    sourceDirs += files("${project.buildDir.path}/swagger-code-java-client/src/main/java")
  }
}