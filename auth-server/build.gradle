plugins {
  id "org.jetbrains.kotlin.jvm"
  id "application"
  id "com.github.johnrengelman.shadow"
  id "idea"
}

dependencies {
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
  implementation "io.dropwizard:dropwizard-core:$dropwizardVersion"

  implementation project(":firebase-extensions")
  implementation ("com.fasterxml.jackson.module:jackson-module-kotlin:$jacksonVersion") {
    exclude group: "org.jetbrains.kotlin", module:"kotlin-reflect"
  }

  runtimeOnly "javax.xml.bind:jaxb-api:$jaxbVersion"
  runtimeOnly "javax.activation:activation:$javaxActivationVersion"
  
  testImplementation "io.dropwizard:dropwizard-testing:$dropwizardVersion"
  testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlinVersion"
  testRuntimeOnly 'org.hamcrest:hamcrest-all:1.3'
}

shadowJar {
  mainClassName = 'org.ostelco.auth.AuthServerApplicationKt'
  mergeServiceFiles()
  archiveClassifier = "uber"
  archiveVersion = ""
}

task pack(type: Zip, dependsOn: 'shadowJar') {
  from('config/') {
    into(project.name + '/config')
  }
  from('script/') {
    into(project.name + '/script')
  }
  from('build/libs/auth-server-uber.jar') {
    into(project.name + '/build/libs/')
  }
  from('Dockerfile') {
    into(project.name)
  }
  archiveName = "auth-server.zip"
  destinationDir = file('build/deploy/')
}

sourceSets {
  integration {
    kotlin {
      compileClasspath += main.output + test.output
      runtimeClasspath += main.output + test.output
      srcDirs += file('src/integration-test/kotlin')
    }
    resources.srcDir file('src/integration-test/resources')
  }
}

configurations {
  integration
  integrationImplementation.extendsFrom testImplementation
  integrationCompile.extendsFrom testCompile
  integrationRuntime.extendsFrom testRuntime
}

task integration(type: Test, description: 'Runs the integration tests.', group: 'Verification') {
  testClassesDirs = sourceSets.integration.output.classesDirs
  classpath = sourceSets.integration.runtimeClasspath
}

integration.mustRunAfter test
build.dependsOn integration

apply from: '../gradle/jacoco.gradle'

idea {
  module {
    testSourceDirs += file('src/integration-test/kotlin')
  }
}