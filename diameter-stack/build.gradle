plugins {
  id "org.jetbrains.kotlin.jvm" version "1.2.70"
  id "java-library"
  id "signing"
  id "maven"
}

version = "1.0.0-SNAPSHOT"

dependencies {
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
  implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
  api('org.mobicents.diameter:jdiameter-api:1.7.1-123') {
    exclude module: 'netty-all'
  }
  api('org.mobicents.diameter:jdiameter-impl:1.7.1-123') {
    exclude module: 'netty-all'
    exclude group: 'org.slf4j', module: 'slf4j-log4j12'
    exclude group: 'log4j', module: 'log4j'
  }
  implementation('org.mobicents.diameter:mobicents-diameter-mux-jar:1.7.0.74') {
    exclude module: 'netty-all'
    exclude group: 'org.slf4j', module: 'slf4j-log4j12'
    exclude group: 'log4j', module: 'log4j'
  }
  implementation 'org.slf4j:log4j-over-slf4j:1.7.25'

  testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlinVersion"
  testRuntimeOnly 'org.hamcrest:hamcrest-all:1.3'
  testImplementation 'org.mockito:mockito-all:1.10.19'
}

task javadocJar(type: Jar) {
  classifier = 'javadoc'
  from javadoc
}

task sourcesJar(type: Jar) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

artifacts {
  archives javadocJar, sourcesJar
}

signing {
  sign configurations.archives
}

// For developers not responsible for uploadArchives,
// and for CI server, until is it not setup to do so.
signArchives.enabled = hasProperty("ossrhUsername")
if (! hasProperty("ossrhUsername")) {
  ext.ossrhUsername=""
}
if (! hasProperty("ossrhPassword")) {
  ext.ossrhPassword=""
}

uploadArchives.repositories.mavenDeployer {
  beforeDeployment { deployment -> signing.signPom(deployment) }

  repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
    authentication(userName: ossrhUsername, password: ossrhPassword)
  }

  snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
    authentication(userName: ossrhUsername, password: ossrhPassword)
  }

  pom.project {
    name = 'ostelco diameter stack'
    packaging 'jar'
    // optionally artifactId can be defined here
    description = 'A library to communicate over the Diameter protocol.'
    url 'https://github.com/ostelco/ostelco-core/tree/develop/diameter-stack'

    scm {
      connection 'scm:svn:https://github.com/ostelco/ostelco-core.git'
      developerConnection 'scm:https://github.com/ostelco/ostelco-core'
      url 'https://github.com/ostelco/ostelco-core'
    }

    licenses {
      license {
        name = 'The Apache License, Version 2.0'
        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
      }
    }

    developers {
      developer {
        id 'la3lma'
        name = 'Bj√∏rn Remseth'
        email 'la3lma@gmail.com'
      }
    }
  }
}

apply from: '../jacoco.gradle'