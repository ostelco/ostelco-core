plugins {
  id "org.jetbrains.kotlin.jvm" version "1.2.51"
  id "application"
  id "com.github.johnrengelman.shadow" version "2.0.4"
  id "idea"
}

sourceSets {
  test {
    java.srcDirs = ['src/test/kotlin']
  }

  integration {
    java.srcDirs = ['src/test/kotlin', 'src/integration-tests/kotlin']
    resources.srcDir 'src/integration-tests/resources'
    compileClasspath += main.output + test.output
    runtimeClasspath += main.output + test.output
  }
}

version = "1.6.1"
dependencies {
  // interface module between prime and prime-modules
  implementation project(':prime-api')

  // prime-modules
  runtimeOnly project(':ocs')
  runtimeOnly project(':firebase-store')
  runtimeOnly project(':embedded-neo4j-store')
  runtimeOnly project(':client-api')
  runtimeOnly project(':admin-api')
  runtimeOnly project(':app-notifier')

  implementation "com.fasterxml.jackson.module:jackson-module-kotlin:$jacksonVersion"
  implementation "io.dropwizard:dropwizard-http2:$dropwizardVersion"
  implementation project(':ostelco-lib')
  implementation "io.dropwizard:dropwizard-json-logging:$dropwizardVersion"
  implementation 'com.google.guava:guava:25.1-jre'

  testImplementation "io.dropwizard:dropwizard-testing:$dropwizardVersion"
  testImplementation 'org.mockito:mockito-core:2.18.3'

  integrationImplementation "io.dropwizard:dropwizard-testing:$dropwizardVersion"
  integrationImplementation 'org.mockito:mockito-core:2.18.3'
}

configurations {
  integration
  integrationImplementation.extendsFrom implementation
  integrationImplementation.extendsFrom runtime
  integrationImplementation.extendsFrom runtimeOnly
}

task integration(type: Test, description: 'Runs the integration tests.', group: 'Verification') {
  environment("GOOGLE_APPLICATION_CREDENTIALS", "config/pantel-prod.json")
  testClassesDirs = sourceSets.integration.output.classesDirs
  classpath = sourceSets.integration.runtimeClasspath
}

shadowJar {
  mainClassName = 'org.ostelco.prime.PrimeApplicationKt'
  mergeServiceFiles()
  classifier = "uber"
  zip64 true
  version = null
}

test {
  testLogging {
    exceptionFormat = 'full'
    events "PASSED", "FAILED", "SKIPPED"
  }
}

idea {
  module {
    testSourceDirs += file('src/integration-tests/kotlin')
  }
}