plugins {
  id "application"
  id "jacoco"
  id "com.github.johnrengelman.shadow" version "2.0.4"
  id "org.jetbrains.kotlin.jvm" version "1.2.41"
  id "idea"
  id "project-report"
}

sourceSets {
  test {
    java.srcDirs = ['src/test/kotlin']
  }

  integration {
    java.srcDirs = ['src/test/kotlin', 'src/integration-tests/kotlin']
    resources.srcDir 'src/integration-tests/resources'
    compileClasspath += main.output + test.output
    runtimeClasspath += main.output + test.output
  }
}

version = "1.5.1"
dependencies {
  // interface module between prime and prime-modules
  implementation project(':prime-api')

  // prime-modules
  runtimeOnly project(':ocs')
  runtimeOnly project(':firebase-store')
  runtimeOnly project(':client-api')
  runtimeOnly project(':admin-api')
  runtimeOnly project(':in-memory-store')

  // removing io.grpc:grpc-netty-shaded:1.10.1 causes ALPN error
  implementation 'io.grpc:grpc-netty-shaded:1.10.1'

  implementation 'com.fasterxml.jackson.module:jackson-module-kotlin:2.9.5'
  implementation "io.dropwizard:dropwizard-http2:$dropwizardVersion"
  implementation project(':ostelco-lib')
  implementation "io.dropwizard:dropwizard-json-logging:$dropwizardVersion"
  implementation 'com.google.guava:guava:25.1-jre'

  // for integration tests
  testImplementation "io.dropwizard:dropwizard-testing:$dropwizardVersion"
  testImplementation 'org.mockito:mockito-core:2.18.3'
  testImplementation project(':ocs')
  testImplementation project(':firebase-store')

  integrationImplementation project(':ocs')
  integrationImplementation project(':firebase-store')
}

configurations {
  integrationImplementation.extendsFrom testImplementation
  integrationCompile.extendsFrom testCompile
  integrationRuntime.extendsFrom testRuntime
}

task integration(type: Test, description: 'Runs the integration tests.', group: 'Verification') {
  environment("GOOGLE_APPLICATION_CREDENTIALS", "config/pantel-prod.json")
  testClassesDirs = sourceSets.integration.output.classesDirs
  classpath = sourceSets.integration.runtimeClasspath
}

shadowJar {
  mainClassName = 'org.ostelco.prime.PrimeApplicationKt'
  mergeServiceFiles()
  classifier = "uber"
  version = null
}

jacocoTestReport {
  group = "Reporting"
  description = "Generate Jacoco coverage reports after running tests."
  additionalSourceDirs = files(sourceSets.main.allJava.srcDirs)
  reports {
    xml.enabled = true
    html.enabled = true
  }
}

check.dependsOn jacocoTestReport

test {
  testLogging {
    exceptionFormat = 'full'
    events "PASSED", "FAILED", "SKIPPED"
  }
}

idea {
  module {
    testSourceDirs += file('src/integration-tests/kotlin')
  }
}