plugins {
  id "org.jetbrains.kotlin.jvm"
  id "application"
  // Keeping version to 4.0.1 - https://github.com/johnrengelman/shadow/issues/425
  // This issue is NOT fixed in version > 4.0.1 including 5.0.0.
  id "com.github.johnrengelman.shadow" version "4.0.1"
  id "idea"
}

sourceSets {
  test {
    java.srcDirs = ['src/test/kotlin']
  }

  integration {
    java.srcDirs = ['src/test/kotlin', 'src/integration-tests/kotlin']
    resources.srcDir 'src/integration-tests/resources'
    compileClasspath += main.output + test.output
    runtimeClasspath += main.output + test.output
  }
}

version = "1.48.7"

repositories {
  maven {
    url 'https://dl.bintray.com/palantir/releases' // docker-compose-rule is published on bintray
  }
}

dependencies {
  // interface module between prime and prime-modules
  api project(':prime-modules')

  // prime-modules
  runtimeOnly project(':ocs-ktc')
  runtimeOnly project(':firebase-store')
  runtimeOnly project(':neo4j-store')
  runtimeOnly project(':customer-endpoint')
  runtimeOnly project(':ekyc')
  runtimeOnly project(':graphql')
  runtimeOnly project(':admin-api')
  runtimeOnly project(':app-notifier')
  runtimeOnly project(':email-notifier')
  runtimeOnly project(':payment-processor')
  runtimeOnly project(':analytics-module')
  runtimeOnly project(':slack')
  runtimeOnly project(':imei-lookup')
  runtimeOnly project(':jersey')
  runtimeOnly project(':secure-archive')
  runtimeOnly project(':scaninfo-datastore')
  runtimeOnly project(':sim-administration:simmanager')

  implementation "io.dropwizard:dropwizard-http2:$dropwizardVersion"
  runtimeOnly "io.dropwizard:dropwizard-json-logging:$dropwizardVersion"
  implementation "com.fasterxml.jackson.module:jackson-module-kotlin:$jacksonVersion"
  implementation "com.google.guava:guava:$guavaVersion"

  testImplementation "io.dropwizard:dropwizard-testing:$dropwizardVersion"
  testImplementation "org.mockito:mockito-core:$mockitoVersion"
  testImplementation 'com.lmax:disruptor:3.4.2'
  testImplementation "com.palantir.docker.compose:docker-compose-rule-junit4:$dockerComposeJunitRuleVersion"
  testImplementation 'org.dhatim:dropwizard-prometheus:2.2.0'
}

configurations {
  integration
  integrationImplementation.extendsFrom implementation
  integrationImplementation.extendsFrom runtime
  integrationImplementation.extendsFrom runtimeOnly
  integrationImplementation.extendsFrom testImplementation
}

task integration(type: Test, description: 'Runs the integration tests.', group: 'Verification') {
  environment("GOOGLE_APPLICATION_CREDENTIALS", "config/prime-service-account.json")
  testClassesDirs = sourceSets.integration.output.classesDirs
  classpath = sourceSets.integration.runtimeClasspath
}

build.dependsOn integration

shadowJar {
  mainClassName = 'org.ostelco.prime.PrimeApplicationKt'
  mergeServiceFiles()
  classifier = "uber"
  zip64 true
  version = null
}

task version {
  println version
}

test {
  testLogging {
    exceptionFormat = 'full'
    events "PASSED", "FAILED", "SKIPPED"
  }
}

idea {
  module {
    testSourceDirs += file('src/integration-tests/kotlin')
  }
}
