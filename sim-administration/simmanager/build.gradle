plugins {
    id "org.jetbrains.kotlin.jvm"
    id "idea"
    id "com.github.johnrengelman.shadow"
    id "com.google.protobuf"
}

dependencies {

    implementation project(":prime-modules")

    implementation project(":sim-administration:jersey-json-schema-validator")
    implementation project(":sim-administration:simcard-utils")
    implementation project(":sim-administration:es2plus4dropwizard")
    implementation project(":sim-administration:ostelco-dropwizard-utils")

    // Arrow
    api "io.arrow-kt:arrow-core:$arrowVersion"
    api "io.arrow-kt:arrow-typeclasses:$arrowVersion"
    api "io.arrow-kt:arrow-instances-core:$arrowVersion"
    api "io.arrow-kt:arrow-effects:$arrowVersion"

    // Grpc
    api "io.grpc:grpc-netty-shaded:$grpcVersion"
    api "io.grpc:grpc-protobuf:$grpcVersion"
    api "io.grpc:grpc-stub:$grpcVersion"
    api "io.grpc:grpc-core:$grpcVersion"
    implementation "com.google.protobuf:protobuf-java:$protocVersion"
    implementation "com.google.protobuf:protobuf-java-util:$protocVersion"
    implementation "javax.annotation:javax.annotation-api:$javaxAnnotationVersion"

    implementation "com.fasterxml.jackson.module:jackson-module-kotlin:$jacksonVersion"

    // Dropwizard
    implementation "io.dropwizard:dropwizard-core:$dropwizardVersion"
    implementation "io.dropwizard:dropwizard-client:$dropwizardVersion"
    implementation "io.dropwizard:dropwizard-jdbi3:$dropwizardVersion"
    implementation "io.dropwizard.metrics:metrics-core:$metricsVersion"
    implementation ("com.google.guava:guava:$guavaVersion") {
        force = true
    }

    implementation "io.opentracing.contrib.dropwizard:dropwizard-opentracing:$dropwizardOpentracingVersion"
    implementation "io.opentracing:opentracing-api:0.33.0"
    implementation "io.jaegertracing:jaeger-client:1.0.0"
    implementation "io.opentracing.contrib:opentracing-jaxrs2-discovery:0.5.0"

    // CSV
    implementation "org.apache.commons:commons-csv:$csvVersion"

    // Jdbi3
    //
    // For dropwizard ver. 1.3.9:
    //   dropwizard-jdbi3 ver. 1.3.9 pulls in jdbi3-core ver. 3.5.1
    //   jdbi3-postgres ver. 3.5.1 pulls in org.postgresql ver. 42.2.2
    // But latest ver. of jdbi3-core is 3.8.2, and for that version
    // jdbi3-postgres no longer pulls in org.postgresql
    //
    // Dropwizard matching version:
    //    jdbi3Version = "3.5.1"
    //    postgresqlVersion = "42.2.2"
    // With latest:
    //    jdbi3Version = "3.8.2"
    //    postgresqlVersion = "42.2.5"
    //
    implementation "org.jdbi:jdbi3-kotlin:$jdbi3Version"
    implementation "org.jdbi:jdbi3-kotlin-sqlobject:$jdbi3Version"
    implementation "org.jdbi:jdbi3-postgres:$jdbi3Version"
    implementation "org.postgresql:postgresql:$postgresqlVersion"

    testImplementation "io.dropwizard:dropwizard-testing:$dropwizardVersion"
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:$mockitoKotlinVersion"
    testImplementation "org.testcontainers:postgresql:$testcontainersVersion"

    testImplementation project(":sim-administration:sm-dp-plus-emulator")
}

sourceSets {
    integration {
        kotlin {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDirs += file('src/integration-test/kotlin')
        }
        resources.srcDir file('src/integration-test/resources')
    }
}

configurations {
    integration
    integrationImplementation.extendsFrom testImplementation
    integrationCompile.extendsFrom testCompile
    integrationRuntime.extendsFrom testRuntime
}

protobuf {
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:$grpcVersion"
        }
    }

    protoc { artifact = "com.google.protobuf:protoc:$protocVersion" }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}


task integration(type: Test, description: 'Runs the integration tests.', group: 'Verification') {
    testClassesDirs = sourceSets.integration.output.classesDirs
    classpath = sourceSets.integration.runtimeClasspath
}

build.dependsOn integration

apply from: '../../gradle/jacoco.gradle'

idea {
    module {
        testSourceDirs += file('src/integration-test/kotlin')
        sourceDirs += files("${protobuf.generatedFilesBaseDir}/main/java")
        sourceDirs += files("${protobuf.generatedFilesBaseDir}/main/grpc")
    }
}


