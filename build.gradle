// these are needed only in top-level module
plugins {
  id "java"
  id "project-report"
  id "com.github.ben-manes.versions" version "0.21.0"
  id "jacoco"
  id "org.jetbrains.kotlin.jvm" version "1.3.31" apply false
}

allprojects {
  apply plugin: 'jacoco'

  group = 'org.ostelco'
  version = '1.0.0-SNAPSHOT'

  repositories {
    mavenCentral()
    jcenter()
    maven { url = "http://repository.jboss.org/nexus/content/repositories/releases/" }
    maven { url = "https://maven.repository.redhat.com/ga/" }
    maven { url = "http://clojars.org/repo/" }
    maven { url "https://jitpack.io" }
  }

  jacoco {
    toolVersion = "0.8.2"
  }
}

subprojects {
  sourceCompatibility = 12
  targetCompatibility = 12
  tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
  }
  ext {
    assertJVersion = "3.12.2"
    arrowVersion = "0.8.2"
    beamVersion = "2.13.0"
    csvVersion = "1.7"
    cxfVersion = "3.3.2"
    dockerComposeJunitRuleVersion = "0.35.0"
    dropwizardVersion = "1.3.12"
    metricsVersion = "4.1.0"
    firebaseVersion = "6.8.1"
    googleCloudVersion = "1.77.0"
    grpcVersion = "1.21.0"
    guavaVersion = "27.1-jre"
    jacksonVersion = "2.9.9"
    javaxActivationVersion = "1.1.1"
    javaxActivationApiVersion = "1.2.0"
    javaxAnnotationVersion = "1.3.2"
    // Keeping it version 1.16.1 to be consistent with grpc via PubSub client lib
    // Keeping it version 1.16.1 to be consistent with netty via Firebase lib
    jaxbVersion = "2.3.1"
    jdbi3Version = "3.8.2"
    jjwtVersion = "0.9.1"
    junit5Version = "5.4.2"
    kotlinVersion = "1.3.31"
    kotlinXCoroutinesVersion = "1.2.1"
    mockitoVersion = "2.28.2"
    mockitoKotlinVersion = "2.1.0"
    neo4jDriverVersion="1.7.5"
    neo4jVersion="3.5.6"
    postgresqlVersion = "42.2.5"  // See comment in ./sim-administration/simmanager/build.gradle
    protocVersion="3.7.1" // updating to "3.8.0" causes compile errors in grpc generated code
    slf4jVersion="1.7.26"
    stripeVersion = "10.0.2"
    swaggerVersion = "2.0.8"
    swaggerCodegenVersion = "2.4.5"
    testcontainersVersion = "1.11.3"
    tinkVersion = "1.2.2"
    zxingVersion = "3.4.0"
  }

  tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
      jvmTarget = "1.8"
    }
  }
}

dependencyUpdates.resolutionStrategy {
  componentSelection { rules ->
    rules.all { ComponentSelection selection ->
      boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'redhat'].any { qualifier ->
        selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
      }
      if (rejected) {
        selection.reject('Release candidate')
      }
    }
  }
}
